managed implementation in class zcl_bp_i_ga_trav_bs unique;
strict ( 1 );
with draft;

define behavior for ZI_GA_TRAV_BS alias Travel
draft table zga_travd_bs
persistent table zga_trav_bs
lock master total etag Lastchangedat
authorization master ( instance, global )
etag master LocalLastChangedAt
{
  create;
  update;
  delete( features : global );
  association _Booking { create( features : instance ); with draft; }

  field ( numbering : managed, readonly ) TravelUuid;
  field ( readonly  ) Status, TravelId, TotalPrice;
  field ( mandatory ) AgencyId, CustomerId;
//  field ( mandatory:create, readonly : update ) AgencyId, CustomerId;
// Actions
  action (features : instance) acceptTravel result[1] $self ;
  action (features : instance) rejectTravel result[1] $self ;
  internal action recalcTotalPrice;

// Determination
 determination setInitialStatus on modify { create; }
 determination setTravelId on save { create; }
 side effects { field BookingFee affects field TotalPrice; }
 determination setTotalPrice on modify { field BookingFee; }


// Validations
 validation validateAgency on save { field AgencyId; create; }
 draft determine action Prepare {
    validation validateAgency;
 }
 draft action Edit;
 draft action Activate optimized;
 draft action Discard;
 draft action Resume;

  mapping for zga_trav_bs
    {
      TravelUuid         = travel_uuid;
      TravelId           = travel_id;
      AgencyId           = agency_id;
      CustomerId         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      Status             = status;
      Createdby          = createdby;
      Createdat          = createdat;
      Lastchangedby      = lastchangedby;
      Lastchangedat      = lastchangedat;
      LocalLastChangedAt = local_last_changed_at;
    }
}
define behavior for ZI_GA_BOOK_BS alias Booking
implementation in class zcl_bp_i_ga_book_bs unique
draft table zga_bookd_bs
persistent table zga_book_bs
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{

  field ( numbering : managed, readonly ) BookingUuid;
  update;
  delete;
  field ( readonly ) TravelUuid;
  association _Travel { with draft; }

// Determination
 side effects { field FlightPrice affects field _Travel.TotalPrice; }
 determination setTotalPrice on modify { field FlightPrice; }
  mapping for zga_book_bs
    {
      BookingUuid        = booking_uuid;
      TravelUuid         = travel_uuid;
      TravelId           = travel_id;
      BookingId          = booking_id;
      BookingDate        = booking_date;
      CustomerId         = customer_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      CurrencyCode       = currency_code;
      Createdby          = createdby;
      Createdat          = createdat;
      Lastchangedby      = lastchangedby;
      Lastchangedat      = lastchangedat;
      LocalLastChangedAt = local_last_changed_at;
    }
}